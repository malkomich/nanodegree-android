apply plugin: 'com.android.application'
apply plugin: 'android-apt'

// Load private properties from file
def privatePropertiesFile = rootProject.file("private.properties")
def privateProperties = new Properties()
def tmdbApiKey;
if (privatePropertiesFile.canRead()) {
    privateProperties.load(new FileInputStream(privatePropertiesFile))

    if (privateProperties.containsKey('tmdbApiKey')) {
        tmdbApiKey = privateProperties['tmdbApiKey']
    }
}



android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"
    defaultConfig {
        applicationId "com.github.malkomich.nanodegree"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            /**
             * IMPORTANT !!!
             * Replace 'tmdbApiKey' variable by your API key for 'The Movie Database'
             */
            resValue "string", "tmdbApiKey", tmdbApiKey
        }
        debug {
            /**
             * IMPORTANT !!!
             * Replace 'tmdbApiKey' variable by your API key for 'The Movie Database'
             */
            resValue "string", "tmdbApiKey", tmdbApiKey
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // App compatibility library
    compile "com.android.support:support-v4:${supportLibraryVersion}"
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:design:${supportLibraryVersion}"
    // Image loading & caching
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:support-vector-drawable:24.2.1'
    // Joda time
    compile group: 'joda-time', name: 'joda-time', version: '2.9.4'
    // ButterKnife
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    // HTTP client
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    // Test libraries
    testCompile 'junit:junit:4.12'
}
